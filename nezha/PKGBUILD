# Maintainer: ZenQy <zenqy.qin@gmail.com>

pkgbase='nezha'
pkgname=('nezhac' 'nezhas')
pkgver=0.4.2
pkgrel=1
pkgdesc="便携服务器状态监控面板"
arch=('x86_64')
url="https://github.com/naiba/nezha"
license=('unknown')
depends=('glibc')
makedepends=('go')

source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "client.conf"
        "config.yaml"
        "nezhac.service"
        "nezhas.service")

sha256sums=('aa00d3be05746db194754fdf83c8a53c5a1a8f5ff73e4d48d1f2716fc80a9db2'
            '366317fb940ab89e3122c8753636219dba018128b7b880a83fabff05f33ab363'
            '00d59d5b2183f02b223cb4c985893017983df09bd33eed73487d58fedfcb8132'
            '44981c063c7b868fbd859df9570f56ebbc9f0d0a3e3d3856e2a68486086eb7ac'
            '1d4c6957c5582588e60b08c20e2b3996af20b8eea63980b4b36cf81b657aaac3')

prepare(){
  cd "${pkgbase}-${pkgver}"
  sed -i 's/data\//\/etc\/nezha\//g' cmd/dashboard/main.go
  sed -i 's/resource\//\/usr\/share\/nezha\//g' cmd/dashboard/controller/controller.go
}

build() {
  cd "${pkgbase}-${pkgver}"
  # export GOPROXY="https://goproxy.cn,direct"
  export CGO_CFLAGS="${CFLAGS} -Wno-return-local-addr"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie"
  go build -o nezhas cmd/dashboard/main.go
  go build -ldflags "-X main.version=${pkgver}" -o nezhac cmd/agent/main.go
}

package_nezhac() {
  backup=('etc/nezha/client.conf')

  install -Dm 755 "${pkgbase}-${pkgver}/nezhac" "${pkgdir}/usr/bin/nezhac"
  install -Dm 644 client.conf "${pkgdir}/etc/nezha/client.conf"
  install -Dm 644 nezhac.service "${pkgdir}/usr/lib/systemd/system/nezhac.service"
}

package_nezhas() {
  backup=('etc/nezha/config.yaml')

  install -Dm 755 "${pkgbase}-${pkgver}/nezhas" "${pkgdir}/usr/bin/nezhas"
  install -Dm 644 config.yaml "${pkgdir}/etc/nezha/config.yaml"
  install -Dm 644 nezhas.service "${pkgdir}/usr/lib/systemd/system/nezhas.service"
  mkdir -p "${pkgdir}/usr/share/nezha"
  ls "${pkgbase}-${pkgver}"
  cp -r "${pkgbase}-${pkgver}/resource/"* "${pkgdir}/usr/share/nezha"
}

