pkgname=caddy
pkgver=2.1.1
pkgrel=3
pkgdesc="Fast, multi-platform web server with automatic HTTPS"
arch=('x86_64')
url="https://caddyserver.com"
license=('Apache')
backup=('etc/caddy/Caddyfile')

source=("https://github.com/caddyserver/caddy/releases/download/v${pkgver}/caddy_${pkgver}_linux_amd64.tar.gz"
        index.html
        caddy.service
        caddy.tmpfiles
        caddy.sysusers
        Caddyfile)

sha256sums=('b136df4d25d4c3f7d5efa55c13f1eb586c22cc8bf595641d9b7f9b7cf8fbf401'
            'f79facc440ace861334e7f3bb4c27a233f06b54b83634e09fcf458c79194a0ec'
            '5d3469563aeecaa0519bec826ad6fd3dcd5ddcf8a06b2837824be32b8a7c5540'
            '1de8079e97c172d30a731f9f3a2da94fcd6d41a7aadd8da3589f9a7a00a10bb1'
            'e69ae147c6b7eb92fe428c29c3ed96bfd4ab62d5fb7de3bf65ad7ffccde66df6'
            '4fae65f9c166b6929bcce25520902916c3fe437e2cf87712927e357b89bcd6cc')

package() {
  install -Dm 755 caddy "$pkgdir/usr/bin/caddy"
  install -Dm 644 "${srcdir}/caddy.service" -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 "${srcdir}/caddy.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/caddy.conf"
  install -Dm 644 "${srcdir}/caddy.sysusers" "${pkgdir}/usr/lib/sysusers.d/caddy.conf"
  install -Dm 644 "${srcdir}/Caddyfile" "${pkgdir}/etc/caddy/Caddyfile"
  install -Dm 644 "${srcdir}/index.html" "${pkgdir}/usr/share/caddy/index.html"
}

